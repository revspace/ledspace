#!/usr/bin/perl -w
use strict;
use Graphics::ColorUtils qw(hsv2rgb);
use Time::HiRes qw(sleep time);

my $num_leds = 13 * 36;
my $time_limit = shift || 9e9;
my $fade_step = .02;
my $shift_step = 2;
my $shift_stop_chance = .005;
my @leds;

# Poor man's enum
use constant MODE => 0;
use constant H => 1;
use constant S => 2;
use constant V => 3;

$| = 1;

sub frame {
    my $frame = "";
    for my $i (0 .. ($num_leds - 1)) {
        my $led = $leds[$i];
        $frame .= $led
            ? pack "C*", hsv2rgb($led->[H], $led->[S], $led->[V])
            : "\0\0\0";
    }
    return $frame;
}

sub random_mode {
    my ($not) = @_;

    my $mode = $not;
    $mode = ('fade_in', 'fade_out', 'shift_up', 'shift_down')[rand 4]
        until $mode ne $not;
    return $mode;
}

# Fade out
$SIG{ALRM} = sub {
    my $alive = 1;

    while ($alive) {
        $alive = 0;

        for my $led (@leds) {
            $led or next;
            $led->[V] -= $fade_step;
            $led->[V] = 0 if $led->[V] < 0;
            $alive = 1 if $led->[V];
        }
            
        print frame;
        sleep .01;
    }
    exit;
};
alarm $time_limit if $time_limit;

# Cycle
while (1) {
    $leds[rand $num_leds] //= [ 'fade_in', int(rand 360), 1, 0 ];

    for my $led (@leds) {
        $led or next;
        my $mode = $led->[MODE] or next;
        if ($mode eq 'fade_in') {
            $led->[V] += $fade_step;
            if ($led->[V] >= 1) {
                $led->[V] = 1;
                $led->[MODE] = random_mode('fade_in');
            }
        } elsif ($mode eq 'fade_out') {
            $led->[V] -= $fade_step;
            if ($led->[V] <= 0) {
                $led->[V] = 0;
                $led->[MODE] = random_mode('fade_out');
            }
        } elsif ($mode eq 'shift_up') {
            ($led->[H] += $shift_step) %= 360;
            $led->[MODE] = random_mode('shift_up') if rand() < $shift_stop_chance;
        } elsif ($mode eq 'shift_down') {
            ($led->[H] -= $shift_step) %= 360;
            $led->[MODE] = random_mode('shift_up') if rand() < $shift_stop_chance;
        }
    }
    print frame;
    sleep .01;
}
